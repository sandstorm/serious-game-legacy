prototype(Sandstorm.ComponentLibrary:Component.Slider) < prototype(Neos.Fusion:Component) {
    slides = ''

    @propTypes {
        @strict = true
        slides = ${PropTypes.string}
    }

    @private {
        inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend
        prevSlideMessage = ${I18n.translate('MyVendor.AwesomeNeosProject:Base:prevSlideMessage')}
        nextSlideMessage = ${I18n.translate('MyVendor.AwesomeNeosProject:Base:nextSlideMessage')}
    }

    // This slider uses swiper.js: https://swiperjs.com/swiper-api
    // The "swiper" css classes are prescribed by swiper.js.

    renderer = afx`
        <MyVendor.AwesomeNeosProject:Utility.ContentWidth>
            <div x-data={AlpineJS.xData('slider', private.prevSlideMessage, private.nextSlideMessage, private.inBackend)}>
                <div class="swiper mb-8" x-ref="slider">
                    {props.slides}

                    <div class={AtomicFusion.classNames(
                        'swiper-pagination',
                        '[&.swiper-pagination-bullets.swiper-pagination-horizontal]:relative [&.swiper-pagination-bullets.swiper-pagination-horizontal]:b-0',
                        '[&_.swiper-pagination-bullet]:bg-grey-light',
                        '[&_.swiper-pagination-bullet.swiper-pagination-bullet-active]:bg-primary-main'
                    )}></div>

                    <button class="swiper-button-prev hidden md:block border-none bg-transparent text-primary-main">
                        <span class="sr-only">
                            {I18n.translate('MyVendor.AwesomeNeosProject:Base:prevSlideMessage')}
                        </span>
                    </button>
                    <button class="swiper-button-next hidden md:block border-none bg-transparent text-primary-main">
                        <span class="sr-only">
                            {I18n.translate('MyVendor.AwesomeNeosProject:Base:nextSlideMessage')}
                        </span>
                    </button>
                </div>
            </div>
        </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
    `
}
