prototype(Sandstorm.ComponentLibrary:Component.Download) < prototype(Neos.Fusion:Component) {
    fileUrl = ''
    description = ''
    align = ''
    fileSize = ''
    type = ''

    @propTypes {
        @strict = true
        fileUrl = ${PropTypes.string}
        description = ${PropTypes.string}
        align = ${PropTypes.oneOf(['left', 'center', 'right'])}
        fileSize = ${PropTypes.string}
        type = ${PropTypes.oneOf(['primary', 'secondary', 'outline', 'link'])}
    }

    @private {
        inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend
        buttonClasses = ${AtomicFusion.classNames(
            'flex items-center gap-2 no-underline cursor-pointer transition focus:outline focus:outline-primary-main',
            {
                'h-8 py-0 px-4 justify-center whitespace-nowrap overflow-hidden border': props.type == 'primary' || props.type == 'secondary' || props.type == 'outline',
                'border-primary-main bg-primary-main text-white hover:text-white hover:border-primary-dark hover:bg-primary-dark focus:text-white focus:border-primary-dark focus:bg-primary-dark active:text-white active:border-black active:bg-black': props.type == 'primary',
                'border-grey-main bg-grey-main text-white hover:text-white hover:border-grey-dark hover:bg-grey-dark focus:text-white focus:border-grey-dark focus:bg-grey-dark active:text-white active:border-black active:bg-black': props.type == 'secondary',
                'border-primary-main bg-white text-primary-main hover:border-primary-dark hover:text-primary-dark focus:border-primary-dark focus:text-primary-dark active:border-black active:text-black': props.type == 'outline',
                'text-primary-main hover:text-primary-dark focus:text-primary-dark active:text-primary-dark': props.type == 'link'
            }
        )}
    }

    renderer = afx`
        <MyVendor.AwesomeNeosProject:Component.Button.ButtonWrapper align={props.align}>
            <Neos.Fusion:Tag
                tagName={(props.fileUrl && !private.inBackend) ? 'a' : 'div'}
                attributes.class={private.buttonClasses}
                attributes.href={props.fileUrl ? props.fileUrl : null}
                attributes.target="_blank"
                attributes.title={props.description}
            >
                <span class="icon-arrow-down-to-line" aria-hidden="true"></span>
                <span class={AtomicFusion.classNames(
                    'flex items-center gap-1',
                    {
                        'overflow-hidden whitespace-nowrap': props.type != 'link',
                        'flex-wrap': props.type == 'link'
                    }
                )}>
                    <span class={AtomicFusion.classNames(
                        {'text-ellipsis overflow-hidden whitespace-nowrap': props.type != 'link'}
                    )}>{props.description}</span>
                    <span class="text-xs" @if={props.fileSize}>({props.fileSize})</span>
                </span>
            </Neos.Fusion:Tag>
        </MyVendor.AwesomeNeosProject:Component.Button.ButtonWrapper>
    `
}
