prototype(MyVendor.AwesomeNeosProject:Component.BlogPostPreview) < prototype(Neos.Fusion:Component) {
    title = ''
    teaserText = ''
    href = ''
    isExternalLink = false
    tags = ''
    image = ''

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        teaserText = ${PropTypes.string}
        href = ${PropTypes.string}
        isExternalLink = ${PropTypes.boolean}
        tags = ${PropTypes.string}
        image = ${PropTypes.string}
    }

    @private.inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend

    renderer = afx`
        <article data-testid="blogPost" class="h-full">
            <Neos.Fusion:Tag
                tagName={private.inBackend ? 'div' : 'a'}
                attributes.href={private.inBackend ? null : props.href}
                attributes.class="h-full flex flex-col bg-white shadow-blog-post no-underline text-black"
                attributes.title={I18n.translate('MyVendor.AwesomeNeosProject:Base:linkTo') + ' Blog Post: ' + props.title}
            >
                {props.image}
                <div class="h-full flex flex-col justify-between p-5 text-center flex-1">
                    <div class="flex flex-col items-center">
                        <h3>{props.title}</h3>
                        <div @if.hasTeaserText={props.teaserText} class="overflow-hidden line-clamp-6">
                            {props.teaserText}
                        </div>
                        <div class="blog-post-preview__tags">
                            {props.tags}
                        </div>
                    </div>
                    <MyVendor.AwesomeNeosProject:Component.Button type="primary" align="center" title={I18n.translate('MyVendor.AwesomeNeosProject:Base:learnMore')} additionalClasses="mt-5" withButtonWrapper={false} />
                </div>
            </Neos.Fusion:Tag>
        </article>
    `
}
