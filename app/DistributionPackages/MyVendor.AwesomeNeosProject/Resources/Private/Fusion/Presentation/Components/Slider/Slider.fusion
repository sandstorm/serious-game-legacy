prototype(MyVendor.AwesomeNeosProject:Component.Slider) < prototype(Neos.Fusion:Component) {
    slides = ''

    @propTypes {
        @strict = true
        slides = ${PropTypes.string}
    }

    @private {
        inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend
        prevSlideMessage = ${I18n.translate('MyVendor.AwesomeNeosProject:Base:prevSlideMessage')}
        nextSlideMessage = ${I18n.translate('MyVendor.AwesomeNeosProject:Base:nextSlideMessage')}
    }

    // This slider uses swiper.js: https://swiperjs.com/swiper-api
    // The "swiper" css classes are prescribed by swiper.js.

    renderer = afx`
        <MyVendor.AwesomeNeosProject:Utility.ContentWidth>
            <div x-data={AlpineJS.xData('slider', private.prevSlideMessage, private.nextSlideMessage, private.inBackend)}>
                <div class="swiper content-slider" x-ref="slider">
                    {props.slides}

                    <div class="swiper-pagination content-slider__pagination"></div>

                    <button class="swiper-button-prev swiper-button-prev--basic-slider content-slider__arrow">
                        <span class="sr-only">
                            {I18n.translate('MyVendor.AwesomeNeosProject:Base:prevSlideMessage')}
                        </span>
                    </button>
                    <button class="swiper-button-next swiper-button-next--basic-slider content-slider__arrow">
                        <span class="sr-only">
                            {I18n.translate('MyVendor.AwesomeNeosProject:Base:nextSlideMessage')}
                        </span>
                    </button>
                </div>
            </div>
        </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
    `
}
