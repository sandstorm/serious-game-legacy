prototype(MyVendor.AwesomeNeosProject:Component.Download) < prototype(Neos.Fusion:Component) {
    fileUrl = ''
    description = ''
    align = ''
    fileSize = ''
    type = ''

    @propTypes {
        @strict = true
        fileUrl = ${PropTypes.string}
        description = ${PropTypes.string}
        align = ${PropTypes.oneOf(['left', 'center', 'right'])}
        fileSize = ${PropTypes.string}
        type = ${PropTypes.oneOf(['solid-blue', 'solid-grey', 'outline', 'link'])}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        _inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend

        renderer = afx`
            <div class={AtomicFusion.classNames(
                    'button__wrapper',
                    'button__wrapper--' + props.align
                )}
            >
                <Neos.Fusion:Tag
                    tagName={(props.fileUrl && !props._inBackend) ? 'a' : 'div'}
                    attributes.class={AtomicFusion.classNames('button', 'button--' + props.type, 'download')}
                    attributes.href={props.fileUrl ? props.fileUrl : null}
                    attributes.target="_blank"
                >
                    <span class="icon-arrow-down-to-line download__icon" aria-hidden="true"></span>
                    <span>
                        {props.description}
                        <span class="download__size" @if={props.fileSize}>({props.fileSize})</span>
                    </span>
                </Neos.Fusion:Tag>
            </div>
        `
    }
}
