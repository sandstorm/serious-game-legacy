prototype(MyVendor.AwesomeNeosProject:Component.BlogPostHeader) < prototype(Neos.Fusion:Component) {
    title = ''
    teaserText = ''
    tags = ''
    date = ''
    image = ''

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        teaserText = ${PropTypes.string}
        tags = ${PropTypes.string}
        date = ${PropTypes.instanceOf('DateTimeInterface')}
        image = ${PropTypes.string}
    }

    renderer = afx`
        <MyVendor.AwesomeNeosProject:Utility.ContentWidth>
            <header class="my-10 w-full text-center">
                <h1 class="m-0">{props.title}</h1>
                <div class="flex flex-col justify-center items-center gap-5 my-5 [&>ul]:m-0">
                    <div @if={props.date} class="text-sm">
                        {Date.format(props.date, 'd.m.Y')}
                    </div>
                    {props.tags}
                </div>
                <div class="[&>.image]:mb-5 [&_.image_img]:w-96 [&_.image>span]:w-96">
                    {props.image}
                </div>
                <div @if.hasTeaserText={props.teaserText} class="w-[600px] max-w-full m-auto text-sm text-center">{props.teaserText}</div>
            </header>
        </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
    `
}
