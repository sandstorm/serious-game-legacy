prototype(MyVendor.AwesomeNeosProject:Component.Accordion.Item) < prototype(Neos.Fusion:Component) {
    title = ''
    content = ''

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        content = ${PropTypes.string}
    }

    @private {
        isFirstChild = ${node.identifier == q(node).parents('[instanceof MyVendor.AwesomeNeosProject:Content.Accordion]').children().get(0).identifier}
        accordionItemXData = ${'{
            id: "' + node.identifier + '",
            get expanded() {
                return this.active === this.id' +
            '},
            set expanded(value) {
                this.active = value ? this.id : null
            },
        }'}
    }

    renderer = afx`
        <div
            class="accordion__item"
            :class="expanded ? 'accordion__item--open' : 'accordion__item--closed'"
            x-data={private.accordionItemXData}
        >
            <button
                class="accordion__trigger"
                id={'accordion__trigger-' + node.identifier}
                aria-controls={'accordion__content-container-' + node.identifier}
                x-on:click="expanded = !expanded"
                :aria-expanded="expanded"
            >
                <h3 class="accordion__title">{props.title}</h3>
                <span aria-hidden="true" class="accordion__icon icon-angle-down"></span>
            </button>
            <div
                class="accordion__content-container"
                id={'accordion__content-container-' + node.identifier}
                role="region"
                aria-labelledby={'accordion__trigger-' + node.identifier}
                x-show="expanded"
                x-collapse
                x-cloak={private.isFirstChild ? null : true}
            >
                <div class="accordion__content">
                    {props.content}
                </div>
            </div>
        </div>
    `
}
