prototype(MyVendor.AwesomeNeosProject:Component.Accordion.Item) < prototype(Neos.Fusion:Component) {
    title = ''
    content = ''

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        content = ${PropTypes.any}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        _inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend

        renderer = afx`
            <div
                x-data={'{
                    id: "' + node.identifier + '",
                    get expanded() {
                        return ' + (props._inBackend ? 'true' : 'this.active === this.id') +
                    '},
                    set expanded(value) {
                        this.active = value ? this.id : null
                    },
                }'}
                class="accordion__item"
            >
                <h3 class="accordion__title-container">
                    <Neos.Fusion:Tag
                        tagName={props._inBackend ? 'div' : 'button'}
                        attributes.class="accordion__trigger"
                        attributes.id={'accordion__trigger-' + node.identifier}
                        attributes.aria-controls={'accordion__content-container-' + node.identifier}
                        attributes.x-on:click="expanded = !expanded"
                        attributes.:aria-expanded="expanded"
                    >
                        <span class="accordion__title">{props.title}</span>
                        <span
                            aria-hidden="true" class="accordion__icon"
                            :class="expanded ? 'accordion__icon--open' : 'accordion__icon--closed'"
                        >
                            <i class="icon-angle-down" />
                        </span>
                    </Neos.Fusion:Tag>
                </h3>
                <div
                    class="accordion__content-container"
                    id={'accordion__content-container-' + node.identifier}
                    role="region"
                    aria-labelledby={'accordion__trigger-' + node.identifier}
                    x-show="expanded"
                    x-collapse
                >
                    <div class="accordion__content">
                        {props.content}
                    </div>
                </div>
            </div>
        `
    }
}
