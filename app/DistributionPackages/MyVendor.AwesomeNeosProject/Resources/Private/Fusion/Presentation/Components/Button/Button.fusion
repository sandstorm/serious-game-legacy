prototype(MyVendor.AwesomeNeosProject:Component.Button) < prototype(Neos.Fusion:Component) {
    title = ''
    href = ''
    isExternalLink = false
    type = ''
    align = ''
    icon = ''
    iconAlignment = 'left'
    x-data = null
    x-ref = null
    additionalCssClasses = null

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        href = ${PropTypes.string}
        isExternalLink = ${PropTypes.boolean}
        type = ${PropTypes.oneOf(['solid-blue', 'solid-grey', 'outline', 'link'])}
        align = ${PropTypes.oneOf(['left', 'center', 'right'])}
        icon = ${PropTypes.string}
        iconAlignment = ${PropTypes.oneOf(['left', 'right'])}
        x-data = ${PropTypes.string}
        x-ref = ${PropTypes.string}
        additionalCssClasses = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        _inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend

        _buttonClasses = ${AtomicFusion.classNames(
            'button',
            'button--' + props.type, 
            props.icon ? 'button--with-icon' : null, 
            props.icon ? 'button--icon-' + props.iconAlignment : null, 
            props.additionalCssClasses
        )}

        renderer = afx`
            <div
                class={AtomicFusion.classNames(
                    'button__wrapper',
                    'button__wrapper--' + props.align
                )}
            >
                <Neos.Fusion:Tag
                    tagName={(props.href && !props._inBackend) ? 'a' : 'div'}
                    attributes.class={props._buttonClasses}
                    attributes.href={props.href ? props.href : null}
                    attributes.target={props.isExternalLink ? '_blank' : null}
                    attributes.x-data={props.x-data ? props.x-data : null}
                    attributes.x-ref={props.x-ref ? props.x-ref : null}
                >
                    <span @if.hasIcon={props.icon} class={AtomicFusion.classNames(props.icon)}></span>
                    {props.title}
                </Neos.Fusion:Tag>
            </div>
        `
    }
}
