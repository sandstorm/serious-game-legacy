prototype(MyVendor.AwesomeNeosProject:Component.EventList.Filter) < prototype(Neos.Fusion:Component) {
    tags = null

    @propTypes {
        @strict = true
        tags = ${PropTypes.arrayOf(PropTypes.dataStructure({'title': PropTypes.string, 'isActive': PropTypes.boolean}))}
    }

    @private {
        _additionalFilterButtonAttributes = Neos.Fusion:DataStructure {
            "data-testid"="filterButton"
            "x-on:click"="toggleFilter"
            "x-on:click.outside"="closeFilter"
        }
        _additionalDeleteFilterButtonAttributes = Neos.Fusion:DataStructure {
            "data-testid"="deleteFilterButton"
            "x-on:click"="deleteFilter"
        }
    }

    renderer = afx`
        <div class="relative mb-10 flex gap-4" x-data="eventList">
            <MyVendor.AwesomeNeosProject:Component.Button type="primary" title={I18n.translate('MyVendor.AwesomeNeosProject:Base:filterDates')} additionalAttributes={private._additionalFilterButtonAttributes} withButtonWrapper={false} />
            <div x-show="filterOpen" class="flex flex-col absolute p-5 top-10 bg-white shadow-md" x-cloak>
                <Neos.Fusion:Loop items={props.tags} itemName="tag">
                    <label class="grid grid-cols-[20px_auto] gap-3 mb-2 last:mb-0 text-sm" data-testid="eventFilterTag">
                        <div class="relative w-5 h-5">
                            <input
                                autocomplete="off"
                                type="checkbox"
                                name={tag.title}
                                checked={tag.isActive}
                                id={tag.title}
                                x-on:click="filterByTagClick"
                                class="absolute inset-0 appearance-none grid place-content-center m-0 bg-white border border-grey-light w-5 h-5"
                            />
                            <span class={AtomicFusion.classNames(
                                'absolute inset-0 w-5 h-5 flex justify-center items-center text-primary-main',
                                {'icon-check': tag.isActive}
                            )}></span>
                        </div>
                        <span>{tag.title}</span>
                    </label>
                </Neos.Fusion:Loop>
            </div>
            <MyVendor.AwesomeNeosProject:Component.Button @if={Array.length(Array.filter(props.tags, tag => tag.isActive == true)) != 0} type="link" title={I18n.translate('MyVendor.AwesomeNeosProject:Base:resetFilters')} additionalAttributes={private._additionalDeleteFilterButtonAttributes} withButtonWrapper={false} additionalClasses="!text-xs" />
        </div>
    `
}
