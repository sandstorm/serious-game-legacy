prototype(MyVendor.AwesomeNeosProject:Component.BlogTagItem) < prototype(Neos.Fusion:Component) {
    tag = null
    activeTag = null
    renderAsLink = true

    @propTypes {
        @strict = true
        tag = ${PropTypes.instanceOf('MyVendor.AwesomeNeosProject:Document.Blog.Tag')}
        activeTag = ${PropTypes.instanceOf('MyVendor.AwesomeNeosProject:Document.Blog.Tag')}
        renderAsLink = ${PropTypes.boolean}
    }

    @private.tagPage = Neos.Neos:NodeUri {
        node = ${props.tag}
    }

    renderer = afx`
        <li>
            <Neos.Fusion:Tag
                tagName={props.renderAsLink ? 'a' : 'span'}
                attributes.class={AtomicFusion.classNames('blog-tag', {'blog-tag--is-active': props.activeTag == props.tag})}
                attributes.href={props.renderAsLink ? private.tagPage : null}
                attributes.title={props.renderAsLink ? I18n.translate('MyVendor.AwesomeNeosProject:Base:linkTo') + ' Tag: ' + props.tag.properties.title : null}
            >
                {props.tag.properties.title}
            </Neos.Fusion:Tag>
        </li>
    `
}
