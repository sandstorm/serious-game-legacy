prototype(MyVendor.AwesomeNeosProject:Component.BlogTagItem) < prototype(Neos.Fusion:Component) {
    tag = null
    activeTag = null
    renderAsLink = true

    @propTypes {
        @strict = true
        tag = ${PropTypes.instanceOf('MyVendor.AwesomeNeosProject:Document.Blog.Tag')}
        activeTag = ${PropTypes.instanceOf('MyVendor.AwesomeNeosProject:Document.Blog.Tag')}
        renderAsLink = ${PropTypes.boolean}
    }

    @private.tagPage = Neos.Neos:NodeUri {
        node = ${props.tag}

    }

    renderer = afx`
        <li data-testid="blogTag">
            <a
                @if={props.renderAsLink}
                href={private.tagPage}
                title={I18n.translate('MyVendor.AwesomeNeosProject:Base:linkTo') + ' Tag: ' + props.tag.properties.title}
                class={AtomicFusion.classNames(
                    'button',
                    {'border-primary-main bg-white text-primary-main hover:border-primary-dark hover:text-primary-dark focus:border-primary-dark focus:text-primary-dark active:text-black active:border-black': props.activeTag != props.tag},
                    {'bg-primary-dark border-primary-dark text-white hover:text-white focus:text-white': props.activeTag == props.tag}
                )}
            >{props.tag.properties.title}</a>

            <span @if={!props.renderAsLink} class="lowercase before:content-['#']">{props.tag.properties.title}</span>
        </li>
    `
}
