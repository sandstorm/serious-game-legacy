prototype(MyVendor.AwesomeNeosProject:Component.Map) < prototype(Neos.Fusion:Component) {
    lat = 0
    lat.@process.toFloat = ${String.toFloat(value)}
    lng = 0
    lng.@process.toFloat = ${String.toFloat(value)}
    zoom = 14
    zoom.@process.toFloat = ${String.toFloat(value)}

    popupText = ''

    @propTypes {
        @strict = true
        lat = ${PropTypes.float.isRequired}
        lng = ${PropTypes.float.isRequired}
        zoom = ${PropTypes.float.isRequired}
        popupText = ${PropTypes.string}
    }

    renderer = afx`
        <div class="mb-8">
            <link rel="stylesheet" href="/_maptiles/frontend/v1/map-main.css"/>
            <div
                class="h-[80vh]"
                x-data={AlpineJS.xData('map', {lat: props.lat, lng: props.lng, zoom: props.zoom, popupText: props.popupText})}
                x-intersect.once="loadMap()"
            >
            </div>
        </div>
    `
}
