prototype(MyVendor.AwesomeNeosProject:Component.SectionWithBackground) < prototype(Neos.Fusion:Component) {
    content = ''
    backgroundColor = ''
    contentWidth = ''
    padding = ''

    @propTypes {
        @strict = true
        content = ${PropTypes.string}
        backgroundColor = ${PropTypes.string}
        contentWidth = ${PropTypes.string}
        padding = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        _backgroundColorClass = MyVendor.AwesomeNeosProject:Utility.BackgroundColor {
            backgroundColor = ${props.backgroundColor}
        }
        _paddingClass = ${props.padding == 'none' ? 'background-section--padding-none' : '' }

        renderer = afx`
            <section class={AtomicFusion.classNames('background-section', props._backgroundColorClass, props._paddingClass )}>
                <MyVendor.AwesomeNeosProject:Utility.ContentWidth size={props.contentWidth}>
                    {props.content}
                </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
            </section>
        `
    }
}
