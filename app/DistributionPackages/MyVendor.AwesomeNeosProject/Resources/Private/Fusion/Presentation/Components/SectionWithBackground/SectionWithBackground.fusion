prototype(MyVendor.AwesomeNeosProject:Component.SectionWithBackground) < prototype(Neos.Fusion:Component) {
    content = ''
    backgroundColor = ''
    contentWidth = ''
    padding = ''

    @propTypes {
        @strict = true
        content = ${PropTypes.string}
        backgroundColor = ${PropTypes.string}
        contentWidth = ${PropTypes.string}
        padding = ${PropTypes.string}
    }

    @private.backgroundColorClass = MyVendor.AwesomeNeosProject:Utility.BackgroundColor {
        backgroundColor = ${props.backgroundColor}
    }

    renderer = afx`
        <section class={AtomicFusion.classNames(
            'w-full py-8 md:py-16',
            private.backgroundColorClass,
            {
                'py-0': props.padding == 'none'
            }
        )}>
            <MyVendor.AwesomeNeosProject:Utility.ContentWidth size={props.contentWidth}>
                {props.content}
            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
        </section>
    `
}
