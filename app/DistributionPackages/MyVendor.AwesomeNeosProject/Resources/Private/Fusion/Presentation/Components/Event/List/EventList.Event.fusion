prototype(MyVendor.AwesomeNeosProject:Component.EventList.Event) < prototype(Neos.Fusion:Component) {
    title = ''
    href = ''
    title = ''
    tags = ''
    date = ''
    timeFrom = ''
    timeTo = ''
    location = ''

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        href = ${PropTypes.string}
        tags = ${PropTypes.string}
        date = ${PropTypes.instanceOf('DateTimeInterface')}
        timeFrom = ${PropTypes.string}
        timeTo = ${PropTypes.string}
        location = ${PropTypes.string}
    }

    @private {
        day = ${Date.format(props.date, 'd')}
        month = ${Date.formatCldr(props.date, 'MMM', 'de')}
        year = ${Date.format(props.date, 'Y')}
    }

    renderer = afx`
        <div class="flex mt-5 pb-5 border-b border-black" data-testid="event">
            <div class="flex flex-col items-center w-14 md:w-20 mr-2 md:mr-5">
                <span class="text-5xl leading-none">{private.day}</span>
                <span class="uppercase">{private.month}</span>
                <span>{private.year}</span>
            </div>
            <div>
                <div @if={props.timeFrom} class="text-xs">
                    {props.timeFrom}
                    <span @if={props.timeTo}> - {props.timeTo}</span>
                    {' '}{I18n.translate('MyVendor.AwesomeNeosProject:Base:oClock')}
                </div>
                {props.tags}
                <h4 class="mb-2 font-bold">{props.title}</h4>
                <div @if={props.location}>
                    <span class="icon-location" aria-hidden="true"></span> {props.location}
                </div>
                <!--TODO: Button -->
                <div class="[&>div]:mb-0 [&>div]:mt-5">
                    <MyVendor.AwesomeNeosProject:Component.Button
                        title={I18n.translate('MyVendor.AwesomeNeosProject:Base:learnMore')}
                        href={props.href}
                        type="link"
                        align="left"
                    />
                </div>
            </div>
        </div>
    `
}
