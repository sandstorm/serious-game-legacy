prototype(MyVendor.AwesomeNeosProject:Component.BlogTagList) < prototype(Neos.Fusion:Component) {
    tags = null
    activeTag = null
    renderAsLink = true

    @propTypes {
        @strict = true
        tags = ${PropTypes.arrayOf( PropTypes.instanceOf('MyVendor.AwesomeNeosProject:Document.Blog.Tag') )}
        activeTag = ${PropTypes.instanceOf('MyVendor.AwesomeNeosProject:Document.Blog.Tag')}
        renderAsLink = ${PropTypes.boolean}
    }

    renderer = afx`
        <ul class="flex flex-wrap gap-2 my-5 p-0 text-center list-none">
            <Neos.Fusion:Loop items={props.tags} itemName="tag" @children="itemRenderer">
                <MyVendor.AwesomeNeosProject:Component.BlogTagItem
                    tag={tag}
                    activeTag={props.activeTag}
                    renderAsLink={props.renderAsLink}
                />
            </Neos.Fusion:Loop>
        </ul>
    `
}
