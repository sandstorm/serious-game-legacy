prototype(MyVendor.AwesomeNeosProject:Component.PageMenu.Mega) < prototype(Neos.Fusion:Component) {

    root = ${site}
    menuItems = Neos.Neos:MenuItems {
        maximumLevels = 2
        filter = "
            Neos.Neos:Document,
            !MyVendor.AwesomeNeosProject:Document.NotFoundPage,
            !MyVendor.AwesomeNeosProject:Document.Blog.Tags,
            !MyVendor.AwesomeNeosProject:Document.Blog.Post
        "
    }

    renderer = afx`
        <div class="page-menu__mega " :class="megaMenuVisible ? 'page-menu__mega--visible' : ''">
            <header class="page-menu__mega-header">
                <button
                    x-on:click="megaMenuVisible = false"
                    id="close-mega-menu"
                    class="page-menu__toggle button button--square button--text"
                    title="Menü schließen"
                >
                    <i class="fas fa-times"></i>
                </button>
            </header>

            <div class="page-menu__mega-content-wrapper">
                <div class="page-menu__mega-content">
                    <ul class="page-menu__toplevel">
                        <Neos.Fusion:Loop items={props.menuItems} itemName="item">
                            <li class={item.state + ' page-menu__toplevel-item'}>
                                <Neos.Neos:NodeLink
                                    node={item.node}
                                    attributes.class="page-menu__toplevel-link"
                                >
                                    <h4>{item.label}</h4>
                                </Neos.Neos:NodeLink>
                                <ul
                                    @if.has={item.subItems}
                                    class="page-menu__sublevel"
                                >
                                    <Neos.Fusion:Loop items={item.subItems} itemName="itemLvl2">
                                        <li class={itemLvl2.state + ' page-menu__sublevel-item'}>
                                            <Neos.Neos:NodeLink
                                                node={itemLvl2.node}
                                                attributes.class="page-menu__sublevel-link"
                                            >{itemLvl2.label}</Neos.Neos:NodeLink>
                                            <ul
                                                @if.has={itemLvl2.subItems}
                                                class="page-menu__sublevel"
                                            >
                                                <Neos.Fusion:Loop items={itemLvl2.subItems} itemName="itemLvl3">
                                                    <li class={itemLvl3.state + ' page-menu__sublevel-item'}>
                                                        <Neos.Neos:NodeLink
                                                            node={itemLvl3.node}
                                                            attributes.class="page-menu__sublevel-link"
                                                        >{itemLvl3.label}</Neos.Neos:NodeLink>
                                                    </li>
                                                </Neos.Fusion:Loop>
                                            </ul>
                                        </li>
                                    </Neos.Fusion:Loop>
                                </ul>
                            </li>
                        </Neos.Fusion:Loop>
                    </ul>
                    <!--                        <Sandstorm.Website:PageMenu.PageMenuTeaser showEmptyInBackend={true} additionalClass="page-menu__mega-teaser">-->
                    <!--                            <Sandstorm.Website:PageMenu.Mega.TeaserQuery @path="teaserQuery" />-->
                    <!--                        </Sandstorm.Website:PageMenu.PageMenuTeaser>-->
                </div>
            </div>

        </div>
    `

}
