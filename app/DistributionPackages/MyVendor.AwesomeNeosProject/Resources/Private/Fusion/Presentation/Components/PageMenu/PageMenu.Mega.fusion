prototype(MyVendor.AwesomeNeosProject:Component.PageMenu.Mega) < prototype(Neos.Fusion:Component) {
    @propTypes {
        @strict = true
    }

    @private.menuItems = Neos.Neos:MenuItems {
        maximumLevels = 2
        filter = "
            Neos.Neos:Document,
            !MyVendor.AwesomeNeosProject:Document.NotFoundPage,
            !MyVendor.AwesomeNeosProject:Document.Blog.Tags,
            !MyVendor.AwesomeNeosProject:Document.Blog.Post
        "
    }

    renderer = afx`
        <div class="fixed h-[100vh] w-[100vw] left-0 top-0 z-[999] bg-white [&>div]:h-full" :class="megaMenuOpen ? 'block' : 'hidden'" x-cloak>
            <MyVendor.AwesomeNeosProject:Utility.ContentWidth>

                <header class="absolute top-0 left-0 w-full flex items-center justify-end h-16 px-4 z-[100] [&>div]:block [&>div]:mr-4 [&>div]:mt-[2px] lg:[&>div]:hidden">
                    <MyVendor.AwesomeNeosProject:Component.LanguageMenu/>

                    <button
                        x-on:click="megaMenuOpen = false"
                        class="flex justify-center items-center flex-col p-1 m-0 h-8 w-8 text-2xl text-primary-main hover:text-primary-dark focus:text-primary-dark focus:border focus:border-primary-dark"
                        title={I18n.translate('MyVendor.AwesomeNeosProject:Base:closeMenu')}
                    >
                        <span class="icon-close" aria-hidden="true"></span>
                        <span class="sr-only">{I18n.translate('MyVendor.AwesomeNeosProject:Base:closeMenu')}</span>
                    </button>
                </header>

                <div class="h-full overflow-hidden">
                    <div class="pt-16 h-full flex flex-nowrap justify-between items-start overflow-y-auto page-menu__mega-content">
                        <ul class="m-0 pt-0 px-0 pb-16 list-none flex flex-wrap w-full max-w-full">
                            <Neos.Fusion:Loop items={private.menuItems}>
                                <li class={item.state + ' mb-4 h-auto w-full sm:w-1/2 xl:w-1/3'}>
                                    <Neos.Neos:NodeLink
                                        node={item.node}
                                        attributes.class={AtomicFusion.classNames(
                                            'no-underline text-lg hover:text-primary-main focus:text-primary-main active:text-primary-main',
                                            {
                                                'text-black': item.state != 'current',
                                                'text-primary-main': item.state == 'current'
                                            }
                                        )}
                                    >
                                        {item.label}
                                    </Neos.Neos:NodeLink>
                                    <ul @if.has={item.subItems} class="list-none flex flex-col flex-wrap mt-2 gap-x-4 break-words">
                                        <Neos.Fusion:Loop items={item.subItems}>
                                            <li class={AtomicFusion.classNames(item.state, 'page-menu__sublevel-item')}>
                                                <Neos.Neos:NodeLink
                                                    node={item.node}
                                                    attributes.title={I18n.translate('MyVendor.AwesomeNeosProject:Base:linkToPage') + item.label}
                                                    attributes.class={AtomicFusion.classNames(
                                                        'no-underline hover:text-primary-main focus:text-primary-main active:text-primary-main',
                                                        {
                                                            'text-black': item.state != 'current',
                                                            'text-primary-main': item.state == 'current'
                                                        }
                                                    )}
                                                >
                                                    {item.label}
                                                </Neos.Neos:NodeLink>
                                            </li>
                                        </Neos.Fusion:Loop>
                                    </ul>
                                </li>
                            </Neos.Fusion:Loop>
                        </ul>
                    </div>
                </div>
            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
        </div>
    `
}
