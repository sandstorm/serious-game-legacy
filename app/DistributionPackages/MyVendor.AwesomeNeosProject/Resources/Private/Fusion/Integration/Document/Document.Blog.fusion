prototype(MyVendor.AwesomeNeosProject:Document.Blog) < prototype(MyVendor.AwesomeNeosProject:Document.AbstractPage) {
    body.main.content.blogtags = MyVendor.AwesomeNeosProject:Utility.ContentWidth {
        content = MyVendor.AwesomeNeosProject:Component.BlogTagList {
            tags = ${q(site).find('[instanceof MyVendor.AwesomeNeosProject:Document.Blog.Tag]')}
        }
    }
    body.main.content.blogtags.@position = 'after mainContent'

    body.main.content.blogposts = MyVendor.AwesomeNeosProject:Utility.ContentWidth {
        content = Flowpack.Listable:PaginatedCollection {
            collection = ${q(site).find('[instanceof MyVendor.AwesomeNeosProject:Document.Blog.Post][_hiddenInIndex != true]').sort('datePublished', 'DESC').get()}
            itemsPerPage = 16

            prototype(Flowpack.Listable:Collection) {
                // We need to keep class 'blog-overview' for now because it's needed in the test
                listClass = 'blog-post-overview grid grid-cols-1 sm:grid-cols-2 md:grid-cols-3 lg:grid-cols-4 gap-5 mb-5 p-0 list-none'
                itemClass = 'blog-post-overview__post'

                itemRenderer = MyVendor.AwesomeNeosProject:Content.BlogPostPreview
            }
        }
    }
    body.main.content.blogposts.@position = 'after mainContent'
}
