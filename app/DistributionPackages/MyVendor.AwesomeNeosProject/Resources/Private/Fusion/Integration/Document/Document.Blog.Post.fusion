prototype(MyVendor.AwesomeNeosProject:Document.Blog.Post) < prototype(MyVendor.AwesomeNeosProject:Document.AbstractPage) {
    body.main.class = 'content-width-context--blogpost'

    body.main.content.blogpostheader = MyVendor.AwesomeNeosProject:Component.BlogPostHeader {
        title = ${q(node).property('title')}
        teaserText = MyVendor.AwesomeNeosProject:Helper.DocumentEditableProperty {
            property = 'teaserText'
        }
        date = ${q(node).property('datePublished')}

        image = MyVendor.AwesomeNeosProject:Component.Image {
            alternativeText = MyVendor.AwesomeNeosProject:Helper.ImageAltText {
                image = ${q(node).property('image')}
            }
            imageDescription = ${q(node).property('imageDescription')}
            imageUri = Neos.Fusion:ResourceUri {
                resource = ${q(node).property('image').resource}
            }
            imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
                asset = ${q(node).property('image')}
            }
            fileExtension = ${q(node).property('image').resource.fileExtension}
            align = 'center'
            sizes = ${[
                '(min-width: 400px) 400px',
                '100vw'
            ]}
        }

        tags = MyVendor.AwesomeNeosProject:Component.BlogTagList {
            tags = ${q(node).property('tags')}
        }
    }
    body.main.content.blogpostheader.@position = 'before mainContent'
}
