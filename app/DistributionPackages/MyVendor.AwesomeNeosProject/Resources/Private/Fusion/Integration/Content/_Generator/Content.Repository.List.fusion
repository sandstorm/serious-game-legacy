prototype(MyVendor.AwesomeNeosProject:Content.Repository.List) < prototype(Neos.Neos:ContentComponent) {
    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
            editPreviewMode = ${node.context.currentRenderingMode.name}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTypeTag('MyVendor.AwesomeNeosProject:Document.Repository.Item', node)}
        }
    }

    renderer = MyVendor.AwesomeNeosProject:Component.PreviewTiles {
        content = Neos.Fusion:Loop {
            items = ${q(site).find('[instanceof MyVendor.AwesomeNeosProject:Document.Repository.Item]')}
            itemRenderer = MyVendor.AwesomeNeosProject:Component.PreviewTiles.Tile {
                title = ${q(item).property('title')}
                image = MyVendor.AwesomeNeosProject:Content.Image.Only {
                    @context.node = ${item}
                    sizes = MyVendor.AwesomeNeosProject:Helper.ImageSizes {
                        type = 'three-column--wrap'
                    }
                    pictureClasses = 'inline-block h-full w-full object-cover'
                    imgClasses = 'max-w-full h-full w-full object-cover'
                }
                href = Neos.Neos:NodeUri {
                    node = ${item}
                }
            }
        }
    }
}
